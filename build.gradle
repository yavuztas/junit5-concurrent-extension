plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group 'dev.yavuztas'
version '1.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    runtimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

test {
    useJUnitPlatform()
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

ext {
    isRelease = !String.valueOf(project.version).toLowerCase().contains('snapshot')
    repoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
    snapshotRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
    println "Inferred version ${project.version}"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId = rootProject.name
            version = project.version

            from components.java

            // create the sign pom artifact
            pom.withXml {
                def pomFile = file("${project.buildDir}/generated-pom.xml")
                writeTo(pomFile)
                def pomAscFile = signing.sign(pomFile).signatureFiles[0]
                artifact(pomAscFile) {
                    classifier = null
                    extension = 'pom.asc'
                }
            }

            artifact sourceJar
            artifact javadocJar

            // create the signed artifacts
            project.tasks.signArchives.signatureFiles.each {
                artifact(it) {
                    def matcher = it.file =~ /-(sources|javadoc)\.jar\.asc$/
                    if (matcher.find()) {
                        classifier = matcher.group(1)
                    } else {
                        classifier = null
                    }
                    extension = 'jar.asc'
                }
            }

            pom {
                name = 'Junit5 Concurrent Extension'
                description = 'An extension for Junit5 to execute concurrency tests with ease'
                url = 'https://github.com/yavuztas/junit5-concurrent-extension'

                scm {
                    url = 'https://github.com/yavuztas/junit5-concurrent-extension'
                    connection = 'scm:git:git://github.com/yavuztas/junit5-concurrent-extension.git'
                    developerConnection = 'scm:git:ssh://github.com/yavuztas/junit5-concurrent-extension.git'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'yavuztas'
                        name = 'Yavuz Tas'
                        email = 'hi@yavuztas.dev'
                    }
                }
            }
            repositories {
                maven {
                    url = "${isRelease ? repoUrl : snapshotRepoUrl}"
                    credentials {
                        username "${sonatype_user}"
                        password "${sonatype_password}"
                    }
                }
            }
        }
    }
}

model {
    tasks.generatePomFileForMavenJavaPublication {
        destination = file("$buildDir/generated-pom.xml")
    }
    tasks.publishMavenJavaPublicationToMavenLocal {
        dependsOn project.tasks.signArchives
    }
    tasks.publishMavenJavaPublicationToMavenRepository {
        dependsOn project.tasks.signArchives
    }
}
